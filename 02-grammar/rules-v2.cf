entrypoints Program;
IProgram.Program ::= [Def];
separator Def "";

// Function definitions.
FunDef.Def ::= Typename QualIdent "(" [ArgDecl] ")" FunBody;
InlFunDef.Def ::= "inline" Typename QualIdent "(" [ArgDecl] ")" FunBody;
EmptyFunBody.FunBody ::= ";";
IFunBody.FunBody ::= "{" [Stmt] "}";

// Typedefs.
TypedefDef.Def ::= TypedefStmt;

// Using imports.
UsingDef.Def ::= "using" QualIdent ";";

// Struct definition.
StructDef.Def ::= StructStmt;

// Var declaration definition.
VarDeclDef.Def ::= VarDecl;

// Function arguments.
separator ArgDecl ",";
IArgDecl.ArgDecl ::= Typename ArgSpec;
EmptyArgSpec.ArgSpec ::= ;
IArgSpec.ArgSpec ::= QualIdent ArgDefaultSpec;
EmptyArgDefaultSpec.ArgDefaultSpec ::= ;
IArgDefaultSpec.ArgDefaultSpec ::= "=" Literal;

// Typenames.
ConstTypename.Typename      ::= "const" Typename1;
ReferenceTypename.Typename1 ::= Typename2 "&";
PointerTypename.Typename1   ::= Typename2 "*";
PrimitiveTypename.Typename2 ::= BuiltinPrimitive;
UserTypename.Typename2 ::= QualIdent;
coercions Typename 2;

// Typedef statement.
ITypedefStmt.TypedefStmt ::= "typedef" Typename QualIdent ";";

// Struct definition.
separator nonempty VarDecl "";
EmptyStructStmt.StructStmt ::= "struct" Identifier "{" "}" ";";
IStructStmt.StructStmt ::= "struct" Identifier "{" [VarDecl] "}" ";";

// Variable declarations.
separator nonempty VarDeclInit ",";
IVarDecl.VarDecl ::= Typename [VarDeclInit] ";";
IVarDeclInit.VarDeclInit ::= Identifier;
InitVarDeclInit.VarDeclInit ::= Identifier "=" VarInitVal;
LiteralVarInitVal.VarInitVal ::= Literal;
IdentVarInitVal.VarInitVal ::= QualIdent;

// Statements.
separator Stmt "";
NopStmt.Stmt ::= ";";
BlockStmt.Stmt ::= "{" [Stmt] "}";
VarDeclStmt.Stmt ::= VarDecl;
ExprStmt.Stmt ::= Expr ";";
ReturnStmt.Stmt ::= "return" Expr ";";
WhileStmt.Stmt ::= "while" "(" Expr ")" Stmt;
DoWhileStmt.Stmt ::= "do" Stmt "while" "(" Expr ")" ";";
ForStmt.Stmt ::= "for" "(" VarDecl Expr ";" Expr ")" Stmt;
IfStmt.Stmt ::= "if" "(" Expr ")" Stmt;
IfElseStmt.Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt;

// Expressions.
separator Expr ",";
LiteralExpr.Expr11 ::= Literal;

VarExpr.Expr10 ::= QualIdent;
IndexingExpr.Expr10 ::= Expr "[" Expr "]";
CallExpr.Expr10 ::= Expr "(" [Expr] ")";

DotAccExpr.Expr9 ::= Expr "." Identifier;
ArrAccExpr.Expr9 ::= Expr "->" Identifier;
DerefExpr.Expr9 ::= "*" Expr;
PostIncExpr.Expr9 ::= Expr "++";
PostDecExpr.Expr9 ::= Expr "--";

PreIncExpr.Expr8 ::= "++" Expr;
PreDecExpr.Expr8 ::= "--" Expr;
NegateExpr.Expr8 ::= "!" Expr;

MulExpr.Expr7 ::= Expr "*" Expr;
DivExpr.Expr7 ::= Expr "/" Expr;
ModExpr.Expr7 ::= Expr "%" Expr;

AddExpr.Expr6 ::= Expr "+" Expr;
SubExpr.Expr6 ::= Expr "-" Expr;

LessExpr.Expr5 ::= Expr "<" Expr;
GreaterExpr.Expr5 ::= Expr ">" Expr;
LeqExpr.Expr5 ::= Expr "<=" Expr;
GeqExpr.Expr5 ::= Expr ">=" Expr;

AndExpr.Expr4 ::= Expr "&&" Expr;

OrExpr.Expr3 ::= Expr "||" Expr;

AssExpr.Expr2 ::= Expr "=" Expr;
AddAssExpr.Expr2 ::= Expr "+=" Expr;
SubAssExpr.Expr2 ::= Expr "-=" Expr;

CondExpr.Expr1 ::= Expr "?" Expr ":" Expr;

ThrowExpr.Expr ::= "throw" Expr;

coercions Expr 11;

// ------------------------------------------------------------------
// Literal values.
// ------------------------------------------------------------------
separator String "";
IntLiteral    .Literal ::= Integer;
StringLiteral .Literal ::= [String] String;
DoubleLiteral .Literal ::= Double;
TrueLiteral   .Literal ::= "true";
FalseLiteral  .Literal ::= "false";
// ------------------------------------------------------------------

// Qualified ids.
separator nonempty Typename ",";
FinQualIdent.QualIdent ::= TrailQualIdent;
NestQualIdent.QualIdent ::= Identifier "::" QualIdent;
TemplQualIdent.QualIdent ::= Identifier "<" [Typename] ">" "::" QualIdent;
ITrailQualIdent.TrailQualIdent ::= Identifier;
TemplTrailQualIdent.TrailQualIdent ::= Identifier "<" [Typename] ">";

// ------------------------------------------------------------------
// Builtin primitive types.
// ------------------------------------------------------------------
BoolPrimitiveDatatype   .BuiltinPrimitive ::= "bool";
DoublePrimitiveDatatype .BuiltinPrimitive ::= "double";
IntPrimitiveDatatype    .BuiltinPrimitive ::= "int";
VoidPrimitiveDatatype   .BuiltinPrimitive ::= "void";
// ------------------------------------------------------------------



// ------------------------------------------------------------------
// General identifier structure.
// ------------------------------------------------------------------
token Identifier (letter(letter|digit|'_')*);
// ------------------------------------------------------------------



// Ignore.
comment "//";
comment "/*" "*/";
comment "#";
